apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

// Get keys from file not contained in git
def getProps(String propName) {
    def propsFile = rootProject.file('secrets.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName]
    } else {
        return ""
    }
}

android {
    defaultConfig {
        applicationId "com.aquamorph.frcmanager"
        minSdkVersion 21
        targetSdkVersion 35
        compileSdk 35
        versionCode 29
        versionName "2025.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "TBA_KEY", getProps("TBA_KEY"))
        multiDexEnabled true
    }
    testOptions {
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "APP_DEBUG", "true"
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "APP_DEBUG", "false"
            enableUnitTestCoverage false
            enableAndroidTestCoverage false
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    lint {
        abortOnError false
    }
    namespace 'com.aquamorph.frcmanager'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.2.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.github.apl-devs:appintro:v4.2.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinLibVersion}"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation 'com.android.support:multidex:1.0.3'

    // Android runner and rules support
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'

    // Espresso support
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // add this for intent mocking support
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'

    // add this for webview testing support
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.6.1'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

